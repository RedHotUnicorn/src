---
title: 'GitHub - juji-io/datalevin: A simple, fast and versatile Datalog database'
date: 2022-11-27
src_link: https://www.notion.so/GitHub-juji-io-datalevin-A-simple-fast-and-versatile-Datalog-database-1b998527821e4b128b319a6bd1e1fb4a
src_date: '2022-11-27 09:20:00'
gold_link: https://github.com/juji-io/datalevin
gold_link_hash: d4525d7be484539c88b9615184cbcec5
tags:
- '#host_github_com'
---

[![](/juji-io/datalevin/raw/master/logo.png)](/juji-io/datalevin/blob/master/logo.png)


Datalevin
=========


 üßò Simple, fast and versatile Datalog database for everyone üíΩ 



[![](https://camo.githubusercontent.com/07c2e01aac6eb2254b0fc23eb5827f08da6aedf7e564dea98e8eb0aa3b8b3a2c/68747470733a2f2f636c6a646f632e6f72672f62616467652f646174616c6576696e2f646174616c6576696e)](https://cljdoc.org/d/datalevin/datalevin)
[![](https://camo.githubusercontent.com/7b6c8df7e4e7c85411c385d1e9c4cd4fee20ab268b1983c12b7b444a72910f44/68747470733a2f2f696d672e736869656c64732e696f2f636c6f6a6172732f762f646174616c6576696e2e7376673f636f6c6f723d73756363657373)](https://clojars.org/datalevin)
[![](https://raw.githubusercontent.com/babashka/babashka/master/logo/badge.svg)](https://github.com/juji-io/datalevin/blob/master/doc/install.md#babashka-pod)




[![](https://github.com/juji-io/datalevin/actions/workflows/release.binaries.yml/badge.svg?branch=0.9.5)](https://github.com/juji-io/datalevin/actions)
[![](https://camo.githubusercontent.com/091e6009ffccacebb5f99cc3456a213096fafdfc3b6414f2652980b1a14f5af2/68747470733a2f2f63692e6170707665796f722e636f6d2f6170692f70726f6a656374732f7374617475732f6769746875622f6a756a692d696f2f646174616c6576696e3f7376673d74727565)](https://ci.appveyor.com/project/huahaiy/datalevin)
[![](https://camo.githubusercontent.com/3e4a452f48cb5ba038daa04c5bdc04b4ba10847b76e419cdbb4bcf465d3317d5/68747470733a2f2f6170692e6369727275732d63692e636f6d2f6769746875622f6a756a692d696f2f646174616c6576696e2e737667)](https://cirrus-ci.com/github/juji-io/datalevin)




> I love Datalog, why hasn't everyone used this already?


Datalevin is a simple durable [Datalog](https://en.wikipedia.org/wiki/Datalog)
database. Here's what a Datalog query looks like:



```
(d/q '[:find  ?name ?total
       :in    $ ?year
       :where [?sales :sales/year ?year]
              [?sales :sales/total ?total]
              [?sales :sales/customer ?customer]
              [?customer :customers/name ?name]]
      (d/db conn) 2024)
```

‚ùì Why
-----


The rationale is to have a simple, fast and open source Datalog query engine
running on durable storage.


It is our observation that many developers prefer
the flavor of Datalog popularized by [Datomic¬Æ](https://www.datomic.com) over
any flavor of SQL, once they get to use it. Perhaps it is because Datalog is
more declarative and composable than SQL, e.g. the automatic implicit joins seem
to be its killer feature. In addition, the recursive rules feature of Datalog
makes it suitable for graph processing and deductive reasoning.


The feature set of Datomic¬Æ may be an overkill for some use cases. One thing
that may confuse casual users is its [temporal
features](https://docs.datomic.com/cloud/whatis/data-model.html#time-model). To
keep things simple and familiar, Datalevin behaves the same way as most other
databases: when data are deleted, they are gone.


Datalevin started out as a port of
[Datascript](https://github.com/tonsky/datascript) in-memory Datalog database to
[LMDB](https://en.wikipedia.org/wiki/Lightning_Memory-Mapped_Database) for
persistence. We then added a [cost-based query optimizer](/juji-io/datalevin/blob/master/doc/query.md) to
enhance query performance.


Datalevin can be used as a library, embedded in applications to manage state, e.g.
used like SQLite; or it can run in a networked
[client/server](https://github.com/juji-io/datalevin/blob/master/doc/server.md)
mode (default port is 8898) with full-fledged role-based access control (RBAC)
on the server, e.g. used like Postgres.


Datalevin relies on the robust ACID transactional database features of LMDB.
Designed for concurrent read intensive workloads, LMDB also [performs
well](http://www.lmdb.tech/bench/ondisk/) in writing large values (> 2KB).
Therefore, it is fine to store documents in Datalevin.


Datalevin can be used as a fast key-value store for
[EDN](https://en.wikipedia.org/wiki/Extensible_Data_Notation) data. The native
EDN data capability of Datalevin should be beneficial for Clojure programs.


Moreover, Datalevin has a [built-in full-text search
engine](https://github.com/juji-io/datalevin/blob/master/doc/search.md) that has
[competitive](https://github.com/juji-io/datalevin/tree/master/search-bench)
search performance.


Presentation:


* [2020 London Clojurians Meetup](https://youtu.be/-5SrIUK6k5g)


üöö [Installation](/juji-io/datalevin/blob/master/doc/install.md)
---------------------------------------------------------------


As a Clojure library, Datalevin is simple to add as a dependency to your Clojure
project. There are also several other options. Please see details in [Installation Documentation](/juji-io/datalevin/blob/master/doc/install.md)


üéâ Usage
-------


Datalevin is aimed to be a versatile database.


### Use as a Datalog store


In addition to [our API doc](https://cljdoc.org/d/datalevin/datalevin),
since Datalevin has almost the same Datalog API as Datascript, which in turn has
almost the same API as Datomic¬Æ, please consult the abundant tutorials, guides
and learning sites available online to learn about the usage of Datomic¬Æ flavor
of Datalog.


Here is a simple code example using Datalevin:



```
(require '[datalevin.core :as d])

;; Define an optional schema.
;; Note that pre-defined schema is optional, as Datalevin does schema-on-write.
;; However, attributes requiring special handling need to be defined in schema,
;; e.g. many cardinality, uniqueness constraint, reference type, and so on.
(def schema {:aka  {:db/cardinality :db.cardinality/many}
             ;; :db/valueType is optional, if unspecified, the attribute will be
             ;; treated as EDN blobs, and may not be optimal for range queries
             :name {:db/valueType :db.type/string
                    :db/unique    :db.unique/identity}})

;; Create DB on disk and connect to it, assume write permission to create given dir
(def conn (d/get-conn "/tmp/datalevin/mydb" schema))
;; or if you have a Datalevin server running on myhost with default port 8898
;; (def conn (d/get-conn "dtlv://myname:mypasswd@myhost/mydb" schema))

;; Transact some data
;; Notice that :nation is not defined in schema, so it will be treated as an EDN blob
(d/transact! conn
             [{:name "Frege", :db/id -1, :nation "France", :aka ["foo" "fred"]}
              {:name "Peirce", :db/id -2, :nation "france"}
              {:name "De Morgan", :db/id -3, :nation "English"}])

;; Query the data
(d/q '[:find ?nation
       :in $ ?alias
       :where
       [?e :aka ?alias]
       [?e :nation ?nation]]
     (d/db conn)
     "fred")
;; => #{["France"]}

;; Retract the name attribute of an entity
(d/transact! conn [[:db/retract 1 :name "Frege"]])

;; Pull the entity, now the name is gone
(d/q '[:find (pull ?e [*])
       :in $ ?alias
       :where
       [?e :aka ?alias]]
     (d/db conn)
     "fred")
;; => ([{:db/id 1, :aka ["foo" "fred"], :nation "France"}])

;; Close DB connection
(d/close conn)
```

### Use as a key-value store


Datalevin packages the underlying LMDB database as a convenient key-value store
for EDN data.



```
(require '[datalevin.core :as d])
(import '[java.util Date])

;; Open a key value DB on disk and get the DB handle
(def db (d/open-kv "/tmp/datalevin/mykvdb"))
;; or if you have a Datalevin server running on myhost with default port 8898
;; (def db (d/open-kv "dtlv://myname:mypasswd@myhost/mykvdb" schema))

;; Define some table (called "dbi", or sub-databases in LMDB) names
(def misc-table "misc-test-table")
(def date-table "date-test-table")

;; Open the tables
(d/open-dbi db misc-table)
(d/open-dbi db date-table)

;; Transact some data, a transaction can put data into multiple tables
;; Optionally, data type can be specified to help with range query
(d/transact-kv
  db
  [[:put misc-table :datalevin "Hello, world!"]
   [:put misc-table 42 {:saying "So Long, and thanks for all the fish"
                        :source "The Hitchhiker's Guide to the Galaxy"}]
   [:put date-table #inst "1991-12-25" "USSR broke apart" :instant]
   [:put date-table #inst "1989-11-09" "The fall of the Berlin Wall" :instant]])

;; Get the value with the key
(d/get-value db misc-table :datalevin)
;; => "Hello, world!"
(d/get-value db misc-table 42)
;; => {:saying "So Long, and thanks for all the fish",
;;     :source "The Hitchhiker's Guide to the Galaxy"}


;; Range query, from unix epoch time to now
(d/get-range db date-table [:closed (Date. 0) (Date.)] :instant)
;; => [[#inst "1989-11-09T00:00:00.000-00:00" "The fall of the Berlin Wall"]
;;     [#inst "1991-12-25T00:00:00.000-00:00" "USSR broke apart"]]

;; This returns a PersistentVector - e.g. reads all data in JVM memory
(d/get-range db misc-table [:all])
;; => [[42 {:saying "So Long, and thanks for all the fish",
;;          :source "The Hitchhiker's Guide to the Galaxy"}]
;;     [:datalevin "Hello, world!"]]

;; This allows you to iterate over all DB keys inside a transaction.
;; You can perform writes inside the transaction.
;; kv is of of type https://www.javadoc.io/doc/org.lmdbjava/lmdbjava/latest/org/lmdbjava/CursorIterable.KeyVal.html
;; Avoid long-lived transactions. Read transactions prevent reuse of pages freed by newer write transactions, thus the database can grow quickly.
;; Write transactions prevent other write transactions, since writes are serialized.
;; LMDB advice: http://www.lmdb.tech/doc/index.html
;; Conclusion: It's ok to have long transactions if using a single thread.
(d/visit db misc-table
            (fn [kv]
               (let [k (d/read-buffer (d/k kv) :data)]
                  (when (= k 42)
                    (d/transact-kv db [[:put misc-table 42 "Don't panic"]]))))
              [:all])

(d/get-range db misc-table [:all])
;; => [[42 "Don't panic"] [:datalevin "Hello, world!"]]

;; Delete some data
(d/transact-kv db [[:del misc-table 42]])

;; Now it's gone
(d/get-value db misc-table 42)
;; => nil

;; Close key value db
(d/close-kv db)
```

üìó Documentation
---------------


Please refer to the [API
documentation](https://cljdoc.org/d/datalevin/datalevin) for more details.
You may also consult online materials for Datascript or Datomic¬Æ, as the Datalog API is
similar.


üöÄ Status
--------


Datalevin is extensively tested with property-based testing. It is also used
in production at [Juji](https://juji.io).


Running the [benchmark suite adopted from Datascript](https://github.com/juji-io/datalevin/tree/master/datascript-bench), which write 100K random datoms in several conditions, and run several queries on them, on a Ubuntu Linux server with an Intel i7 3.6GHz CPU and a 1TB SSD drive, here is how it looks.



[![](/juji-io/datalevin/raw/master/datascript-bench/Read.png)](/juji-io/datalevin/blob/master/datascript-bench/Read.png)
[![](/juji-io/datalevin/raw/master/datascript-bench/Write.png)](/juji-io/datalevin/blob/master/datascript-bench/Write.png)



In this benchmark, both Datomic and Datascript are running in in-memory mode, as
they require another database for persistence. The `init` write condition, i.e.
bulk loading prepared datoms, is not available in Datomic. Datalevin write here
is configured with LMDB `nosync` mode to better match the in-memory conditions,
i.e. the operating system is responsible for flushing data to disk.


In all benchmarked queries, Datalevin is the fastest among the three tested
systems, as Datalevin has a [cost based query optimizer](/juji-io/datalevin/blob/master/doc/query.md) while Datascript and
Datomic do not. Datalevin also has a caching layer for index access.


Writes are slower, as expected, as Datalevin does write to disk even though sync
is not explicitly called, while others are purely
in memory. The bulk loading speed is good, writing 100K datoms to disk in less
than 0.2 seconds; the same data can also be transacted with all the integrity
checks as a whole or five datoms at a time in less than 1.5 seconds. Transacting
one datom at a time, it takes longer time. Therefore, it is preferable to have
batch transactions.


See [here](https://github.com/juji-io/datalevin/tree/master/datascript-bench)
for a detailed analysis of the results.


üéÇ Upgrade
---------


Please read
[this](https://github.com/juji-io/datalevin/blob/master/doc/upgrade.md) for
information regarding upgrading your existing database from older versions.


üåé Roadmap
---------


These are the tentative goals that we try to reach as soon as we can. We may
adjust the priorities based on feedback.


* 0.4.0 ~~Native image and native command line tool.~~ [Done 2021/02/27]
* 0.5.0 ~~Native networked server mode with role based access control.~~ [Done 2021/09/06]
* 0.6.0 ~~As a search engine: full-text search across database.~~ [Done 2022/03/10]
* 0.7.0 ~~Explicit transactions, lazy results loading, and results spill to disk when memory is low.~~ [Done 2022/12/15]
* 0.8.0 ~~Long ids; composite tuples; enhanced search engine ingestion speed.~~ [Done 2023/01/19]
* 0.9.0 ~~New Datalog query engine with improved performance.~~ [Done 2024/03/09]
* 1.0.0 New rule evaluation algorithm, incremental view maintenance, documentation in book form.
* 1.1.0 Option to store data in compressed form.
* 2.0.0 Dense numeric vector indexing and similarity search.
* 2.1.0 Transaction log storage and access API.
* 2.2.0 Read-only replicas for server.
* 3.0.0 Automatic document indexing.
* 3.1.0 Fully automatic schema migration on write.
* 4.0.0 [loom](https://github.com/aysylu/loom) graph protocols and common graph algorithms.
* 5.0.0 Distributed mode.


üíæ Differences from Datascript
-----------------------------


Datascript is developed by [Nikita Prokopov](https://tonsky.me/) that "is built
totally from scratch and is not related by any means to" Datomic¬Æ. Datalevin
started out as a port of Datascript to LMDB, but differs from Datascript in more
significant ways than just the difference in data durability and running mode:


* Datalevin has a cost-based query optimizer, so queries are truly declarative
and clause ordering does not affect query performance.
* Datalevin is not an immutable database, and there is no
"database as a value" feature. Since history is not kept, transaction ids are
not stored.
* Datoms in a transaction are committed together as a batch, rather than being
saved by `with-datom` one at a time.
* ACID transaction and rollback are supported.
* Lazy results set and spill to disk are supported.
* Entity and transaction integer ids are 64 bits long, instead of 32 bits.
* Respects `:db/valueType`. Currently, most [Datomic¬Æ value
types](https://docs.datomic.com/on-prem/schema.html#value-types) are
supported, except uri. Values of the attributes that
are not defined in the schema or have unspecified types are treated as
[EDN](https://en.wikipedia.org/wiki/Extensible_Data_Notation) blobs, and are
de/serialized with [nippy](https://github.com/ptaoussanis/nippy).
* In addition to composite tuples, Datalevin also supports heterogeneous and
homogeneous tuples.
* Has a value leading index (VAE) for datoms with `:db.type/ref` type attribute;
The attribute and value leading index (AVE) is enabled for all datoms, so
there is no need to specify `:db/index`, similar to Datomic¬Æ Cloud. Does not
have AEV index, in order to save storage and improve write speed.
* Stored transaction functions of `:db/fn` should be defined with `inter-fn`, for
function serialization requires special care in order to support GraalVM
native image. It is the same for functions that need to be passed over the
wire to server or babashka.
* Attributes are stored in indices as integer ids, thus attributes in index
access are returned in attribute creation order, not in lexicographic order
(i.e. do not expect `:b` to come after `:a`). This is the same as Datomic¬Æ.
* Has no features that are applicable only for in-memory DBs, such as DB as an
immutable data structure, DB pretty print, etc.


üë∂ Limitations
-------------


* Attribute names have a length limitation: an attribute name cannot be more
than 511 bytes long, due to LMDB key size limit.
* Because keys are compared bitwise, for range queries to work as expected on an
attribute, its `:db/valueType` should be specified.
* Floating point `NaN` cannot be stored.
* Big integers do not go beyond the range of `[-2^1015, 2^1015-1]`, the
unscaled value of big decimal has the same limit.
* The maximum individual value size is 2GB. Limited by the maximum size of
off-heap byte buffer that can be allocated in JVM.
* The total data size of a Datalevin database has the same limit as LMDB's, e.g.
128TB on a modern 64-bit machine that implements 48-bit address spaces.
* Currently supports Clojure on JVM 8 or the above, but adding support for other
Clojure-hosting runtime is possible, since bindings for LMDB
exist in almost all major languages and available on most platforms.


üõçÔ∏è Alternatives
---------------


If you are interested in using the dialect of Datalog pioneered by Datomic¬Æ, here are your current options:


* If you need time travel and features backed by the authors of Clojure, you should use [Datomic¬Æ](https://www.datomic.com).
* If you need an in-memory store that has almost the same API as Datomic¬Æ, [Datascript](https://github.com/tonsky/datascript) is for you.
* If you need a graph database, you may try [Asami](https://github.com/threatgrid/asami).
* If you need features such as bi-temporal graph queries, you may try [XTDB](https://github.com/xtdb/xtdb).
* If you need a durable store with some storage choices, you may try [Datahike](https://github.com/replikativ/datahike).
* There was also [Eva](https://github.com/Workiva/eva/), a distributed store, but it is no longer in active development.
* If you need a simple, fast and versatile durable store with a battle tested backend, give [Datalevin](https://github.com/juji-io/datalevin) a try.


üîÉ Contact
---------


We appreciate and welcome your contributions or suggestions. Please feel free to
file issues or pull requests.


If commercial support is needed for Datalevin, talk to us.


You can talk to us in the `#datalevin` channel on [Clojurians Slack](http://clojurians.net/).


License
-------


Copyright ¬© 2020-2024 [Juji, Inc.](https://juji.io).


Licensed under Eclipse Public License (see [LICENSE](/juji-io/datalevin/blob/master/LICENSE)).